apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version "$braspag_release_version"
group 'br.com.braspag.sdk'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("build/outputs/aar/velocity-release.aar")
            groupId
            artifactId 'velocity'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}


Properties properties = new Properties()
File propertiesFile = project.rootProject.file('local.properties')

if (propertiesFile.exists()) {
    properties.load(propertiesFile.newDataInputStream())
}

bintray {
    user = properties.getProperty("bintray.user") ?: System.getenv('BINTRAY_USER')
    key = properties.getProperty("bintray.apikey") ?: System.getenv('BINTRAY_API_KEY')
    publications = ['Production']
    dryRun = false
    publish = true
    override = true

    pkg {
        repo = 'Braspag.Android.Sdk'
        name = 'velocity'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/lmarquesmoreira/Braspag.Android.Sdk.git'
        websiteUrl = 'http://www.braspag.com.br/#solucoes'
    }
}